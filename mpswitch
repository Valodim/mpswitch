#!/usr/bin/env zsh

# local and remote prefix to the same music database
typeset -A prefixes
prefixes[localhost]=''
prefixes[fairycake]='valodim/'

host1=$1 ; [[ -z $host1 ]] && host1='localhost'
host2=$2 ; [[ -z $host2 ]] && host2='fairycake'

# make sure both hosts are reachable!
ping -W 1 -c 1 $host1 > /dev/null || { print "Unreachable host: $host1"; exit }
ping -W 1 -c 1 $host2 > /dev/null || { print "Unreachable host: $host2"; exit }

lprefix=$prefixes[$host1]
rprefix=$prefixes[$host2]

# get local playlist
plist=( ${(f)"$( mpc -h $host1 -f '%file%' playlist )"} )

# strip local prefix
plist=( $rprefix${^${plist#$lprefix}} )

pos=$(mpc -h $host1 -f '%position%' status | head -n 1)

# clear remote playlist
mpc -h $host2 clear -q
# set up new one
print ${(F)plist} | mpc add -h $host2

mpc -h $host1 pause -q
ptime=${${$(mpc -h $host1 status | grep -o '(.*%)')#\(}%\)}

print "playing track: $pos ($ptime)"
mpc -h $host2 -q play $pos
if [[ -n $ptime ]]; then
    mpc -h $host2 -q seek $ptime
fi
